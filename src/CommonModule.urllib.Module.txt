function mkpair(v,c)
    return ?(find(v,c)>0,string.split(v,c,1),pair(v,undefined));
endfunction

//function split(url)
//  Разбивает url на структуру вида:
//    <scheme>://<username>:<password>@<host>:<port>/<path>?<query>#<anchorn>
//
//  Параметры:
//      url <string> -- url строка
//
//  Возвращает:
//      <structure:[
//          scheme <string>,
//          username <string>,
//          password <string>,
//          host <string>,
//          port <number>,
//          path <array>,
//          query <map>,
//          anchorn <string>
//      ]>
//
//  Описание:
//      Если отсутствует какой либо компонент адреса url, он заменяется на
//      пустое значение типа секции.
//      При разборе url декодирует экранированные символы.
//      Метод unsplit собирает полученный результат в исходную строку.
//
function split(url) export
    urlstruct=new Structure("scheme,username,password,host,
                            |port,path,query,anchorn",
                            undefined,undefined,undefined,undefined,
                            undefined,new array(),new map(),undefined);
    if find(url,":")>0 then
        res=mkpair(url,":");
        scheme=res[0];
        tail=res[1];
        urlstruct.scheme=scheme;
        if tail=undefined then
            return urlstruct;
        endif;
    else
        tail=url;
    endif;
    res=mkpair(tail,"#");
    head=res[0];
    anchorn=res[1];
    if anchorn<>undefined then
        urlstruct.anchorn=unquote(anchorn);
    endif;
    res=mkpair(head,"?");
    rest=res[0];
    query=res[1];
    if query<>undefined then
        urlstruct.query=query_decode(query);
    endif;
    if rest="" then
        return urlstruct;
    endif;
    if not mid(rest,1,2)="//" then
        urlstruct.path=path_decode(rest);
    else
        rest=mid(rest,3);
        res=mkpair(rest,"/");
        head=res[0];
        path=res[1];
        if path<>undefined then
            urlstruct.path=path_decode(path);
        endif;
        if head<>"" then
            if find(head,"@")>0 then
                res=mkpair(head,"@");
                userpass=res[0];
                hostport=res[1];
            else
                userpass=undefined;
                hostport=head;
            endif;
            if hostport<>"" then
                res=mkpair(hostport,":");
                host=res[0];
                port=res[1];
                urlstruct.host=unquote(host);
                if port<>undefined and port<>"" then
                    try
                        urlstruct.port=number(port);
                    except
                        msg="URI port must be number";
                        env=new Structure("port,urlstruct",port,urlstruct);
                        raise exception.ValueError(msg,env);
                    endtry;
                endif;
            endif;
            if userpass<>undefined then
                res=mkpair(userpass,":");
                username=res[0];
                password=res[1];
                urlstruct.username=unquote(username);
                if password<>undefined then
                    urlstruct.password=unquote(password);
                endif;
            endif;
        endif;
    endif;
    return urlstruct;
endfunction

// splitschema('type:opaquestring') --> 'type', 'opaquestring'.
function splitschema(url) export
    res=re.match(url,"^([^/:]+):");
    if res<>undefined then
        schema=res.groups[0];
        return pair(lower(schema),mid(url,strlen(schema)));
    endif;
    return pair(undefined,url);
endfunction

// splithost('//host[:port]/path') --> 'host[:port]', '/path'.
function splitnetloc(url) export
    res=re.match(url,"^//([^/?]*)(.*)$)");
    return ?(res<>undefined,res.groups,pair(undefined,url));
endfunction

// splituser('user[:passwd]@host[:port]') --> 'user[:passwd]', 'host[:port]'.
function splituser(host) export
    res=re.match(host,"^(.*)@(.*)$");
    return ?(res<>undefined,
             pair(unquote(res[0]),unquote(res[1])),
             pair(undefined,host)
    );
endfunction

// splitpasswd('user:passwd') -> 'user', 'passwd'.
function splitpasswd(user) export
    res=re.match(user,"^([^:]*):(.*)$");
    return ?(res<>undefined,res.groups,pair(user,undefined));
endfunction

// splitport('host:port') --> 'host', 'port'.
function splitport(host) export
    res=re.match(host,"^(.*):([0-9]+)$");
    return ?(res<>undefined,res.groups,pair(host,undefined));
endfunction

//Split host and port, returning numeric port.
//    Return given default port if no ':' found; defaults to -1.
//    Return numerical port if a valid number are found after ':'.
//    Return None if ':' but not a valid number.
function splitnport(host,defport=-1) export
    res=re.match("^(.*):(.*)$");
    if res<>undefined then
        port=res.groups[1];
        try
            if check.is_false(port) then
                raise exception.ValueError("no digits");
            endif;
            nport=number(port);
        except
            nport=undefined;
        endtry;
        return pair(res.group[0],nport);
    endif;
    return pair(host,defport);
endfunction

// splitquery('/path?query') --> '/path', 'query'.
function splitquery(url) export
    res=re.match(url,"^(.*)\?([^?]*)$");
    return ?(res<>undefined,res.groups,pair(url,undefined));
endfunction

// splittag('/path#tag') --> '/path', 'tag'.
function splittag(url) export
    res=re.match(url,"^(.*)#([^#]*)$");
    return ?(res<>undefined,res.groups,pair(url,undefined));
endfunction

// splitattr('/path;attr1=value1;attr2=value2;...') ->
//      '/path', ['attr1=value1', 'attr2=value2', ...]
function splitattr(url) export
    words=string.split(url,";");
    path=words[0];
    words.delete(0);
    return pair(path,words);
endfunction

// splitvalue('attr=value') --> 'attr', 'value'
function splitvalue(attr) export
    res=re.match(attr,"^([^=]*)=(.*)$");
    return ?(res<>undefined,res.groups,pair(attr,undefined));
endfunction

function unsplit(url) export
    if not check.hasattr(url,"scheme,username,password,
                             |host,port,path,anchorn") then
        msg="Invalid url structure given";
        env=new Structure("url",url);
        raise exception.ValueError(msg,env);
    endif;

    if url["scheme"]<>undefined then
        try
            res=url["scheme"]+":";
        except
            msg="URL scheme must be <string> type";
            raise exception.ValueError(msg,url);
        endtry;
    endif;

    if url["username"]<>undefined or url["host"]<>undefined then
        res=res+"//";

        if url["username"]<>undefined then
            res=res+quote(url["username"],"");
            if url["password"]<>undefined then
                res=res+":"+quote(url["password"],"");
            endif;
            res=res+"@";
        endif;

        if url["host"]<>undefined then
            res=res+quote(url["host"],"");
            if url["port"]<>undefined then
                res=res+":"+convert.to_string(url["port"]);
            endif;
        endif;

        res=res+"/";
    endif;

    if url["path"]<>undefined then
        res=res+path_encode(url["path"]);
    endif;

    qs=query_encode(url["query"]);
    if qs<>"" then
        res=res+"?"+qs;
    endif;

    if url["anchorn"]<>undefined then
        res=res+"#"+quote(url["anchorn"],"");
    endif;

    return res;
endfunction

//function join(base,url)
function join(base,url) export
    // Trivial case
    if check.is_false(base) then
        return url;
    elsif check.is_false(url) then
        return base;
    endif;
    //
    if check.is_array_or_fixedarray(url) then
        res=base;
        for each part in url do
            res=join(res,part);
        enddo;
        return res;
    endif;
    //
    if not check.is_string(base) then
        msg="Base url must be string";
        env=new Structure("base,url",base,url);
        raise exception.TypeError(msg,env);
    endif;
    if not check.is_string(url) then
        msg="Joined url must be string";
        env=new Structure("base,url",base,url);
        raise exception.TypeError(msg,env);
    endif;
    //
    schemas=undefined;
    if metadata.commonmodules.find("cache")<>undefined then
        schemas=cache.get("urllib.schemas");
    endif;
    if schemas=undefined then
        schemas=new structure();
        arr=array.create("ftp","hdl","prospero","http","imap","https","shttp",
                         "rtsp","rtspu","sip","sips","mms","","sftp");
        schemas.insert("uses_params",arr);
        arr=array.create("gopher","hdl","mailto","news","telnet","wais","imap",
                         "snews","sip","sips");
        schemas.insert("non_hierarchical",arr);
        arr=array.create("http","wais","imap","https","shttp","mms","gopher",
                         "rtsp","rtspu","sip","sips","");
        schemas.insert("uses_query",arr);
        arr=array.create("ftp","http","gopher","nntp","telnet","imap","wais",
                         "file","mms","https","shttp","snews","prospero","rtsp",
                         "rtspu","rsync","","svn","svn+ssh","sftp","nfs");
        schemas.insert("uses_netloc",arr);
        arr=array.create("ftp","http","gopher","nntp","imap","wais","file","https",
                         "shttp","mms","prospero","rtsp","rtspu","","sftp");
        schemas.insert("uses_relative",arr);
        arr=array.create("ftp","hdl","http","gopher","news","nntp","wais","https",
                         "shttp","snews","file","prospero","");
        schemas.insert("uses_fragment",arr);
    endif;
    if metadata.commonmodules.find("cache")<>undefined then
        cache.set("urllib.schemas",schemas);
    endif;
    bstr=urllib.split(base); //base structure
    jstr=urllib.split(url);  //join structure
    if jstr.scheme=undefined then
        jstr.scheme=bstr.scheme;
    endif;
    if bstr.scheme<>jstr.scheme 
        or schemas["uses_relative"].find(jstr.scheme)=undefined then
        return url;
    endif;
    if schemas["uses_netloc"].find(jstr.scheme)<>undefined then
        if jstr.host<>undefined then
            return urllib.unsplit(jstr);
        endif;
        jstr.username=bstr.username;
        jstr.password=bstr.password;
        jstr.host=bstr.host;
        jstr.port=bstr.port;
    endif;
    if jstr.path.count()=0 then
        jstr.path=bstr.path;
        if jstr.query.count()=0 then
            jstr.query=bstr.query;
        endif;
        return urllib.unsplit(jstr);
    endif;
    if jstr.path[0]="" then
        jstr.path.delete(0);
        return urllib.unsplit(jstr);
    endif;
    if bstr.path.count()>0 then
        bstr.path.delete(bstr.path.ubound());
    endif;
    
    segments=array.merge(bstr.path,jstr.path);
    if segments[segments.ubound()]="." then
        segments[segments.ubound()]="";
    endif;

    while True do
        pos=segments.find(".");
        if pos=undefined then
            break;
        endif;
        segments.delete(pos);
    enddo;

    while True do
        i=1;
        n=segments.ubound();
        while i<n do
            if segments[i]=".." 
                and (segments[i-1]<>"" or segments[i-1]<>"..") then
                segments.delete(i);
                segments.delete(i-1);
                break;
            endif;
            i=i+1;
        enddo;
        if i>=n then
            break;
        endif;
    enddo;
    if segments.count()=2 and segments[0]="" and segments[1]=".." then
        segments[1]="";
    elsif segments.count()>=2 and segments[segments.ubound()]=".." then
        segments.delete(segments.ubound());
        segments[segments.ubound()]="";
    endif;
    jstr.path=segments;
    return urllib.unsplit(jstr);
endfunction

/////////////////////////////////////////////////////////////////////////////

function query_encode(q) export
    res="";
    for each p in q do
        res=res+"&"+quote(p.key);
        if typeof(p.value)=type("boolean") then
            res=res+"="+?(p.value,"true","false");
        elsif check.is_array_or_fixedarray(p.value) then
            for each item in p.value do
                if typeof(item)=type("boolean") then
                    res=res+"[]="+?(item,"true","false");
                else
                    res=res+"[]="+quote(convert.to_string(item));
                endif;
            enddo;
        else
            if ValueIsFilled(p.value) then
                res=res+"="+quote(convert.to_string(p.value));
            endif;
        endif;
    enddo;
    return mid(res,2);
endfunction

function query_decode(q) export
    res=new map();
    for each item in string.split(q,"&") do
        if item<>"" then
            l=mkpair(item,"=");
            res.insert(unquote(l[0]),?(l[1]=undefined,"",unquote(l[1])));
        endif;
    enddo;
    return res;
endfunction

/////////////////////////////////////////////////////////////////////////////

function path_encode(path) export
    res="";
    l=path.ubound();
    if l>=0 then
        res=res+path[0];
        for i=1 to l do
            res=res+"/"+quote(path[i],"");
        enddo;
    endif;
    return res;
endfunction

function path_decode(path) export
    res=new Array();
    for each item in string.split(path,"/") do
        res.add(unquote(item));
    enddo;
    return res;
endfunction

/////////////////////////////////////////////////////////////////////////////

//quote('abc def') -> 'abc%20def'
//
//    Each part of a URL, e.g. the path info, the query, etc., has a
//    different set of reserved characters that must be quoted.
//
//    RFC 2396 Uniform Resource Identifiers (URI): Generic Syntax lists
//    the following reserved characters.
//
//    reserved    = ";" | "/" | "?" | ":" | "@" | "&" | "=" | "+" |
//                  "$" | ","
//
//    Each of these characters is reserved in some component of a URL,
//    but not necessarily in all of them.
//
//    By default, the quote function is intended for quoting the path
//    section of a URL.  Thus, it will not encode '/'.  This character
//    is reserved, but in typical usage the quote function is being
//    called on a path where the existing slash characters are used as
//    reserved characters.
//
function quote(s,safe="/") export
    always_safe="ABCDEFGHIJKLMNOPQRSTUVWXYZ
                |abcdefghijklmnopqrstuvwxyz
                |0123456789_.-";
    if typeof(s)<>type("string") then
        raise exception.TypeError("Argument 1 (s) must be <sting> type",
                                   new Structure("s,safe",s,safe));
    endif;
    l=strlen(s);
    res="";
    for i=1 to l do
        c=mid(s,i,1);
        if find(always_safe,c)<>0 or c=safe then
            res=res+c;
        else
            cc=charcode(c);
            if cc<128 then
                res=res+convert.to_hex(cc,"%");
            elsif cc>127 and cc<2048 then
                // res += hex((cc >> 6) | 192);
                //res=res+num(cc).rshift(6).or_(192).to_hex("%").dispatch();
                res=res+convert.to_hex(bitwise.or_(bitwise.rshift(cc,6),192),"%");
                // res += hex((cc & 63) | 128);
                //res=res+num(cc).and_(63).or_(128).to_hex("%").dispatch();
                res=res+convert.to_hex(bitwise.or_(bitwise.and_(cc,63),128),"%");
            else
                // res += hex((cc >> 12) | 224);
                //res=res+num(cc).rshift_(12).or_(224).to_hex("%").dispatch();
                res=res+convert.to_hex(bitwise.or_(bitwise.rshift(cc,12),224),"%");
                // res += hex(((cc >> 6) & 63) | 128);
                //res=res+num(cc).rshift_(6).and_(63).or_(128).to_hex("%").dispatch();
                res=res+convert.to_hex(bitwise.or_(bitwise.and_(bitwise.rshift(cc,6),63),128),"%");
                // res += hex((cc & 63) | 128);
                ////res=res+num(cc).and_(63).or_(128).to_hex("%").dispatch();
                res=res+convert.to_hex(bitwise.or_(bitwise.and_(cc,63),128),"%");
            endif;
        endif;
    enddo;
    return res;
endfunction

/////////////////////////////////////////////////////////////////////////////

//unquote('abc%20def') -> 'abc def'
function unquote(s) export
    enc="";
    res=string.split(s,"%");
    l=res.ubound();
    for i=1 to l do
        item=res[i];
        code=convert.to_dec("0x"+mid(item,1,2));
        enc=enc+char(code);
        if strlen(item)=2 then
            res[i]="";
        else
            res[i]=string.utf8decode(enc)+mid(item,3);
            enc="";
        endif;
    enddo;
    if enc<>"" then
        res[i-1]=string.utf8decode(enc);
    endif;
    return string.join("",res);
endfunction

/////////////////////////////////////////////////////////////////////////////

// unquote('%7e/abc+def') -> '~/abc def'
function unquote_plus(s) export
    return unquote(strreplace(s,"+"," "));
endfunction