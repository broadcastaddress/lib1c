//function and_(val a,val b)
//    Реализует операцию побитового И для значений a и b.
//
//    Параметры:
//        a,b -- число, представленное в десятичной системе как <number>,
//                в 2-ичной, 8-ричной или 16-ричной системах как <string>
//                или же бинарный массив как <array>,<fixedarray>
//
//    Возвращает:
//        <binaryArray>
function and_(val a,val b) export
    a=convert.to_binarray(a);
    b=convert.to_binarray(b);
    res=new Array();
    i=a.ubound();
    j=b.ubound();
    while i>0 or j>0 do
        an=?(i<1,a[0],a[i]);
        bn=?(j<1,b[0],b[j]);
        res.insert(0,an and bn);
        i=i-1;j=j-1;
    enddo;
    res.insert(0,a[0] and b[0]);
    return res;
endfunction

/////////////////////////////////////////////////////////////////////////////

//function or_(val a,val b)
//    Реализует операцию побитового ИЛИ для значений a и b.
//
//    Параметры:
//        a,b -- число, представленное в десятичной системе как <number>,
//                в 2-ичной, 8-ричной или 16-ричной системах как <string>
//                или же бинарный массив как <array>,<fixedarray>
//
//    Возвращает:
//        <binaryArray>
function or_(val a,val b) export
    a=convert.to_binarray(a);
    b=convert.to_binarray(b);
    res=new Array();
    i=a.ubound();
    j=b.ubound();
    while i>0 or j>0 do
        an=?(i<1,a[0],a[i]);
        bn=?(j<1,b[0],b[j]);
        res.insert(0,an or bn);
        i=i-1;j=j-1;
    enddo;
    res.insert(0,a[0] or b[0]);
    return res;
endfunction

/////////////////////////////////////////////////////////////////////////////

//function xor(val a,val b)
//    Реализует побитовую операцию исключающего ИЛИ для значений a и b.
//
//    Параметры:
//        a,b -- число, представленное в десятичной системе как <number>,
//                в 2-ичной, 8-ричной или 16-ричной системах как <string>
//                или же бинарный массив как <array>,<fixedarray>
//
//    Возвращает:
//        <binaryArray>
function xor(val a,val b) export
    a=convert.to_binarray(a);
    b=convert.to_binarray(b);
    res=new Array();
    i=a.ubound();
    j=b.ubound();
    while i>0 or j>0 do
        an=?(i<1,a[0],a[i]);
        bn=?(j<1,b[0],b[j]);
        res.insert(0,an<>bn);
        i=i-1;j=j-1;
    enddo;
    res.insert(0,a[0]<>b[0]);
    return res;
endfunction

/////////////////////////////////////////////////////////////////////////////

//function not_(value)
//    Реализует операцию побитового НЕ для значения value
//
//    Параметры:
//        value -- число, представленное в десятичной системе как <number>,
//                в 2-ичной, 8-ричной или 16-ричной системах как <string>
//                или же бинарный массив как <array>,<fixedarray>
//
//    Возвращает:
//        <binaryArray>
function not_(value) export
    return inv(value);
endfunction

/////////////////////////////////////////////////////////////////////////////

function lshift(value,shift) export
    res=convert.to_binarray(value);
    if res=value then
        res=_copy(value);
    endif;
    for i=1 to shift do
        res.add(res[0]);
    enddo;
    return res;
endfunction

/////////////////////////////////////////////////////////////////////////////

function rshift(value,shift) export
    res=convert.to_binarray(value);
    if res=value then
        res=_copy(value);
    endif;
    l=res.count()-shift;
    l=?(l<1,1,l);
    while res.count()<>l do
        res.delete(res.ubound());
    enddo;
    return res;
endfunction

/////////////////////////////////////////////////////////////////////////////

function inv(value) export
    res=convert.to_binarray(value);
    if res=value then
        res=_copy(value);
    endif;
    l=res.ubound();
    for i=0 to l do
        res.set(i,not res[i]); // why set?
    enddo;
    return res;
endfunction

function _copy(arr)
    res=new valuestorage(arr);
    return res.get();
endfunction